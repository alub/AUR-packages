# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=decklink-ffmpeg-git
pkgver=20121010
pkgrel=1
pkgdesc='Basic capture and play programs for Blackmagic Design Decklink'
arch=('i686' 'x86_64')
url='http://git.lubineau.net/decklink-ffmpeg/'
license=('custom')
depends=('ffmpeg')
makedepends=('decklink-sdk' 'git')
source=('git://git.lubineau.net/decklink-ffmpeg#branch=archlinux')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  git log -1 --format="%cd" --date=short | sed 's|-||g'
}

build() {
  cd "$srcdir/${pkgname%-git}"
  make
}

package() {
  cd "$srcdir/${pkgname%-git}"

  install -D bmdcapture "$pkgdir/usr/bin/bmdcapture"
  install -D bmdplay "$pkgdir/usr/bin/bmdplay"
  install -D bmdgenlock "$pkgdir/usr/bin/bmdgenlock"

  install -D -m 0644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
