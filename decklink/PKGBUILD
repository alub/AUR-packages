# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=decklink
_basever=9.1
_dlid=3225412
_mever=3.0.3
pkgver=${_basever}a20
pkgrel=3
pkgdesc="Drivers for Blackmagic Design DeckLink, Intensity or Multibridge video editing cards"
arch=('i686' 'x86_64')
url="http://www.blackmagic-design.com/products/"
license=('custom')
depends=('linux-headers' 'libxml2' 'mesa' 'qt' 'gcc-libs')
options=('!strip')
install='decklink.install'
source=("http://www.blackmagic-design.com/media/${_dlid}/Blackmagic_Desktop_Video_Linux_${_basever}.tar.gz"
        'decklink.conf'
        'corrections.patch')
sha256sums=('32749ea64bbc04e654a5ce4d184eaf72c629251adbdec950fefa4f0818266233'
            '49c6f841e7f0d663bc426552159eba1610f880259db9fee6f2e2bf62988358b7'
            '10488ec2bc4b47584529717edcd9adb750e3f4d6d7ed3dd32b58a1b5f24ff12e')
package() {
  # license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s /usr/share/doc/desktopvideo/License.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # automatic module loading
  install -D -m 644 decklink.conf "$pkgdir/etc/modules-load.d/decklink.conf"

  cd "$srcdir"
  [ "$CARCH" = "i686" ] && _arch='i386' && _debarch='i386'
  [ "$CARCH" = "x86_64" ] && _arch='x86_64' && _debarch='amd64'

  # Driver
  tar xf "desktopvideo-${_basever}-${_arch}.tar.gz"
  cd "desktopvideo-$pkgver-${_arch}/usr/src/desktopvideo-$pkgver"
  patch -p0 < "$srcdir/corrections.patch"
  cd "$srcdir"
  cp -a "desktopvideo-$pkgver-${_arch}/"* "$pkgdir"
  [ "$CARCH" = "i686" ] && rm "$pkgdir/usr/lib/blackmagic/libQt"* 
  [ "$CARCH" = "x86_64" ] && rm "$pkgdir/usr/lib64/blackmagic/libQt"*

  # MediaExpress
  ar xf "MediaExpress-${_mever}-${_debarch}.deb"
  tar xf data.tar.gz -C "$pkgdir"
}

# vim:set ts=2 sw=2 et:
