# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=decklink
_basever=9.0
_dlid=2432845
_mever=3.0.3
pkgver=${_basever}a15
pkgrel=2
pkgdesc="Drivers for Blackmagic Design DeckLink, Intensity or Multibridge video editing cards"
arch=('i686' 'x86_64')
url="http://www.blackmagic-design.com/products/"
license=('custom')
depends=('linux-headers' 'libxml2' 'mesa' 'qt' 'gcc-libs')
options=('!strip')
install=('decklink.install')
source=("http://www.blackmagic-design.com/media/${_dlid}/Blackmagic_Desktop_Video_Linux_${_basever}.tar.gz"
        'decklink.conf'
        'linux-3.1.patch')
sha256sums=('cb4fa5217156cce76338079b137746fbf6a60e2f6ebfbc9b680b62c7d6c02056'
            '49c6f841e7f0d663bc426552159eba1610f880259db9fee6f2e2bf62988358b7'
            'e22d0a2d7d5bcd2020d97f8a70f08980f1804f02e7ba3c1d6b14594f71170700')

package() {
  # license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s /usr/share/doc/desktopvideo/License.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # automatic module loading
  install -D -m 644 decklink.conf "$pkgdir/etc/modules-load.d/decklink.conf"

  cd "$srcdir"
  [ "$CARCH" = "i686" ] && _arch='i386' && _debarch='i386'
  [ "$CARCH" = "x86_64" ] && _arch='x86_64' && _debarch='amd64'

  # Driver
  tar xf "desktopvideo-${_basever}-${_arch}.tar.gz"
  patch -p0 "desktopvideo-$pkgver-${_arch}/usr/src/desktopvideo-$pkgver/blackmagic_core.c" < "$srcdir/linux-3.1.patch"
  cp -a "desktopvideo-$pkgver-${_arch}/"* "$pkgdir"
  rm "$pkgdir/usr/lib64/blackmagic/libQt"*

  # MediaExpress
  ar xf "MediaExpress-${_mever}-${_debarch}.deb"
  tar xf data.tar.gz -C "$pkgdir"
}

# vim:set ts=2 sw=2 et:
