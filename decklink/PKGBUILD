# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=decklink
_dvver=10.3.1a5 # DesktopVideo
_mever=3.3.2a3 # MediaExpress
pkgver=${_dvver}
pkgrel=1
pkgdesc="Drivers for Blackmagic Design DeckLink, Intensity or Multibridge video editing cards"
arch=('i686' 'x86_64')
url="http://www.blackmagic-design.com/products/"
license=('custom')
depends=('linux-headers' 'libxml2' 'libpng12' 'glu' 'qt4')
options=('!strip' 'staticlibs')
install='decklink.install'
source=("http://software.blackmagicdesign.com/DesktopVideo/Blackmagic_Desktop_Video_Linux_${pkgver%a*}.tar.gz")
sha256sums=('82a24743c0049083de29466668c64c1951a8f26976311c30d14aa86c58e31b5b')

[ "$CARCH" = "i686" ] && _arch='i386'
[ "$CARCH" = "x86_64" ] && _arch='x86_64'

package() {
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s /usr/share/doc/desktopvideo/License.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  cd "$srcdir"

  tar xf "DesktopVideo_${_dvver%a*}/other/${_arch}/desktopvideo-${_dvver}-${_arch}.tar.gz"
  cp -a "desktopvideo-${_dvver}-${_arch}/"* "$pkgdir"

  tar xf "MediaExpress_${_mever%a*}/other/${_arch}/mediaexpress-${_mever}-${_arch}.tar.gz"
  cp -a "mediaexpress-${_mever}-${_arch}/"* "$pkgdir"
}

# vim:set ts=2 sw=2 et:
