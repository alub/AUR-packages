# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=decklink
_basever=8.0.1
pkgver=${_basever}rc4
pkgrel=4
pkgdesc="Drivers for Blackmagic DeckLink, Intensity or Multibridge video editing cards"
arch=('i686' 'x86_64')
url="http://www.blackmagic-design.com/products/"
license=('custom')
depends=('linux-headers' 'libxml2' 'mesa' 'qt' 'gcc-libs')
options=('!strip')
install=('decklink.install')
source=("http://www.blackmagic-design.com/media/1380773/Blackmagic_Desktop_Video_Linux_${_basever}.tar.gz"
        'decklink.conf'
        'spinlock.patch')
sha256sums=('1d23b4f7566a86b0d8ff5a137b433171c5e3ae27fb30471c7d61d9cc5920c509'
            '49c6f841e7f0d663bc426552159eba1610f880259db9fee6f2e2bf62988358b7'
            '388d0696f8a6fb8dc8ee6ddf738de1749b154634a89641fbd7762d39ee389dbf')

package() {
  # license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s /usr/share/doc/DesktopVideo/License.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # automatic module loading
  install -D -m 644 decklink.conf "$pkgdir/etc/modules-load.d/decklink.conf"

  cd "$srcdir"
  [ "$CARCH" = "i686" ] && _arch='i386' && _debarch='i386'
  [ "$CARCH" = "x86_64" ] && _arch='x86_64' && _debarch='amd64'

  # Driver
  tar xf "DesktopVideo-${_basever}-${_arch}.tar.gz"
  patch -p0 "DesktopVideo-$pkgver-${_arch}/usr/src/DesktopVideo-$pkgver/blackmagic_lib.c" < "$srcdir/spinlock.patch"
  cp -a "DesktopVideo-$pkgver-${_arch}/"* "$pkgdir"
  rm "$pkgdir/usr/lib64/blackmagic/libQt"*

  # MediaExpress
  ar xf "MediaExpress-2.3.1-${_debarch}.deb"
  tar xf data.tar.gz -C "$pkgdir"
}

# vim:set ts=2 sw=2 et:
