# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=decklink
_basever=8.0.1
pkgver=${_basever}rc4
pkgrel=1
pkgdesc="Drivers for Blackmagic DeckLink, Intensity or Multibridge video editing cards"
arch=('i686' 'x86_64')
url="http://www.blackmagic-design.com/products/"
license=('custom')
depends=('libxml2' 'mesa' 'qt' 'gcc-libs')
options=(!strip)
install=('decklink.install')
source=("http://www.blackmagic-design.com/media/1380773/Blackmagic_Desktop_Video_Linux_${_basever}.tar.gz"
        'decklink.conf')
sha256sums=('1d23b4f7566a86b0d8ff5a137b433171c5e3ae27fb30471c7d61d9cc5920c509'
            '49c6f841e7f0d663bc426552159eba1610f880259db9fee6f2e2bf62988358b7')

package() {
  # license
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s /usr/share/doc/DesktopVideo/License.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # module loading for systemd
  install -D -m 644 decklink.conf "$pkgdir/etc/modules-load.d/decklink.conf"

  cd "$srcdir"
  [ "$CARCH" = "i686" ] && arch='i386'
  [ "$CARCH" = "x86_64" ] && arch='x86_64'
  tar xf "DesktopVideo-${_basever}-$arch.tar.gz"
  cd "DesktopVideo-$pkgver-$arch"
  cp -a * "$pkgdir"
}

# vim:set ts=2 sw=2 et:
