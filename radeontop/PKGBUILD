# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=radeontop
pkgver=0.6
_hash=c987b0a
pkgrel=1
pkgdesc="Radeon cards monitoring utility"
arch=('i686' 'x86_64')
url="https://github.com/clbr/radeontop"
license=('GPL3')
depends=('ncurses' 'xf86-video-ati')
source=("https://github.com/clbr/radeontop/tarball/v$pkgver"
        'Makefile.patch')
sha256sums=('131ed14506335d238c1e38d8a2d97bf751dd3ceed34e2eb5fd6e03a1bbe6237d'
            '07ef0a2652c9bcc60d4e0b86ba5a67d28b9a9a2396c2cd5bfc3409af5fe95ea0')

build() {
  cd "$srcdir/clbr-$pkgname-${_hash}"

  patch Makefile < "$srcdir/Makefile.patch"
  cat > include/version.h << EOF
#ifndef VER_H
#define VER_H

#define VERSION "$pkgver"

#endif
EOF

  make
}

package() {
  cd "$srcdir/clbr-$pkgname-${_hash}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
