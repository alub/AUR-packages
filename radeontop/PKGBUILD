# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=radeontop
pkgver=0.7
pkgrel=1
pkgdesc="Radeon cards monitoring utility"
arch=('i686' 'x86_64')
url="https://github.com/clbr/radeontop"
license=('GPL3')
depends=('ncurses' 'xf86-video-ati')
source=("https://github.com/clbr/radeontop/archive/v${pkgver}.tar.gz")
sha256sums=('57542644d95a05e321223b2137836cc300935d8615110ed134c2d83e3ff0058d')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "s/sbin/bin/" Makefile
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
