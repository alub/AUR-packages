# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=beignet
pkgver=0.8
pkgrel=1
pkgdesc='A GPGPU System for Intel Ivybridge GPUs'
arch=('x86_64')
url='http://cgit.freedesktop.org/beignet/'
license=('LGPL2.1')
depends=('glu' 'libsm' 'libxext' 'mesa' 'ncurses')
makedepends=('clang' 'cmake' 'python2')
conflicts=('opencl-headers')
source=("git://anongit.freedesktop.org/beignet#tag=Release_v${pkgver}"
        'fix-linking-with-tinfo.patch'
        'utests-define-python-interpreter-via-cmake-variable.patch'
        'gbe-fix-terminfo-library-linkage.patch'
        'fix-a-convert-float-to-long-bug.patch')
sha256sums=('SKIP'
            '6a05e5e008fdbcf50d020aa79fc8c532c8761880b8e808d7310cbd8e070be44a'
            'f454ff2b20377b4ee80fa5e0cf2d71e50bd2fb52da68fa436f2e08a2bc0043f7'
            'c6607541a28d1eece4e25bdd082cd03bd6b182602b55b351fd16fd7b74c23c5a'
            'cb6daf7b5a42b1e9be839ab086b27b0fec77ade865ea106f82b509b22b74a7c7')

prepare() {
	cd "$srcdir/$pkgname"
	patch -p1 < "$srcdir/fix-linking-with-tinfo.patch"
	patch -p1 < "$srcdir/utests-define-python-interpreter-via-cmake-variable.patch"
	patch -p1 < "$srcdir/gbe-fix-terminfo-library-linkage.patch"
	patch -p1 < "$srcdir/fix-a-convert-float-to-long-bug.patch"
}

build() {
	mkdir -p "$srcdir/$pkgname/build"
	cd "$srcdir/$pkgname/build"
	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RELEASE \
		-DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2
	make
}

package() {
	cd "$srcdir/$pkgname/build"
	make DESTDIR="$pkgdir/" install
}
