# Maintainer: Antoine Lubineau <antoine@lubignon.info>

pkgname=beignet
pkgver=0.9.3
pkgrel=1
pkgdesc='A GPGPU System for Intel Ivybridge GPUs'
arch=('x86_64')
url='http://cgit.freedesktop.org/beignet/'
license=('LGPL2.1')
depends=('glu' 'libsm' 'libxext' 'mesa' 'ncurses')
makedepends=('clang' 'cmake' 'python2')
conflicts=('opencl-headers')
source=("git://anongit.freedesktop.org/beignet#tag=Release_v${pkgver}")
sha256sums=('SKIP')

build() {
	mkdir -p "$srcdir/$pkgname/build"
	cd "$srcdir/$pkgname/build"
	cmake .. \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=RELEASE \
		-DPYTHON_EXECUTABLE:FILEPATH=/usr/bin/python2
	make
}

package() {
	cd "$srcdir/$pkgname/build"
	make DESTDIR="$pkgdir/" install
}
